{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Race, Class, and Student Debt as a Stratification System\n",
    "\n",
    "### Research team: Charlie Eaton, Adam Goldstein, Laura Hamilton, Frederick Wherry\n",
    "\n",
    "### Enrollment segregation IPEDS data exploration. Code by Charlie Eaton\n",
    "\n",
    "### August 5, 2020"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "quietly {\n",
    "foreach year in 2018 {\n",
    "copy https://nces.ed.gov/ipeds/datacenter/data/EF`year'A_Data_Stata.zip EF`year'A_Data_Stata.zip, replace\n",
    "unzipfile EF`year'A_Data_Stata, replace\n",
    "insheet using ef`year'a_data_stata.csv, clear\n",
    "quietly egen efminort = rowtotal (ef2mort efaiant efnhpit efasiat), missing\n",
    "foreach var in totlt totlm totlw aiant aianm aianw asiat asiam ///\n",
    "asiaw bkaat bkaam bkaaw hispt hispm hispw whitt whitm whitw ///\n",
    "unknt unknm unknw 2mort 2morm 2morw nralt nralm nralw nhpit ///\n",
    "nhpim nhpiw minort {\n",
    "quietly gen ftfirstef`var'=ef`var' if line == 1\n",
    "quietly gen ptfirstef`var'=ef`var' if line == 15\n",
    "quietly gen allfirstef`var'=ef`var' if efalevel==4\n",
    "quietly gen ftallef`var'=ef`var' if line == 14\n",
    "quietly gen ptunderef`var'=ef`var' if line == 22\n",
    "quietly gen ptprofef`var'=ef`var' if line == 23\n",
    "quietly gen ptgradef`var'=ef`var' if line == 25\n",
    "quietly gen ftunderef`var'=ef`var' if line == 8\n",
    "quietly gen ftprofef`var'=ef`var' if line == 9\n",
    "quietly gen ftgradef`var'=ef`var' if line == 11\n",
    "}\n",
    "**REPLACE ZERO VALUES WITH SMALL NEGATIVE VALUES**\n",
    "foreach group in totlt totlm totlw aiant aianm aianw asiat asiam ///\n",
    "asiaw bkaat bkaam bkaaw hispt hispm hispw whitt whitm whitw ///\n",
    "unknt unknm unknw 2mort 2morm 2morw nralt nralm nralw nhpit ///\n",
    "nhpim nhpiw minort {\n",
    "foreach level in ftfirst ptfirst allfirst ftall ptunder ptprof ptgrad ftunder ftprof ftgrad {\n",
    "quietly bysort unitid `level'ef`group' : gen miss`level'ef`group' = missing(`level'ef`group'[1])\n",
    "}\n",
    "}\n",
    "**COLLAPSE ENROLLMENT VARIABLES TO A SINGLE ROW FOR ALL INSTITUTIONS**\n",
    "collapse (min) miss* (sum) ft* pt* all*, by(unitid)\n",
    "\n",
    "foreach group in totlt totlm totlw aiant aianm aianw asiat asiam ///\n",
    "asiaw bkaat bkaam bkaaw hispt hispm hispw whitt whitm whitw ///\n",
    "unknt unknm unknw 2mort 2morm 2morw nralt nralm nralw nhpit ///\n",
    "nhpim nhpiw minort {\n",
    "foreach level in ftfirst ptfirst allfirst ftall ptunder ptprof ptgrad ftunder ftprof ftgrad {\n",
    "quietly replace `level'ef`group'=. if miss`level'ef`group'\n",
    "}\n",
    "}\n",
    "    \n",
    "drop miss* ptprof* ftprof*\n",
    "save fteenrollment`year', replace\n",
    "\n",
    "copy https://nces.ed.gov/ipeds/datacenter/data/HD`year'_Data_Stata.zip HD`year'_Data_Stata.zip, replace\n",
    "unzipfile HD`year'_Data_Stata, replace\n",
    "quietly insheet using hd`year'_data_stata.csv, clear\n",
    "quietly sort unitid\n",
    "quietly local m1=(`year'+1)\n",
    "quietly save hd_`m1', replace\n",
    "}\n",
    "use hd_2019, clear\n",
    "merge 1:1 unitid using fteenrollment2018, nogen\n",
    "save hdfallenroll2019, replace\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "quietly {\n",
    "    use hdfallenroll2019.dta, clear\n",
    "drop ftall* ftunder* pt* allfirst* ftgrad*\n",
    "set line 125\n",
    "ds, alpha varwidth(20)\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## A. State level segregation measure\n",
    "\n",
    "### Average percentage of students of other race at school for average student of a given race"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "quietly {\n",
    "use hdfallenroll2019, clear\n",
    "gen wxblackpctall=ftfirstefwhitt *(ftfirstefbkaat / ftfirsteftotlt)\n",
    "gen wxblackpct4yr=ftfirstefwhitt *(ftfirstefbkaat / ftfirsteftotlt) if iclevel==1\n",
    "gen wxblackpct2yr=ftfirstefwhitt *(ftfirstefbkaat / ftfirsteftotlt) if iclevel==2\n",
    "\n",
    "gen wxlatinpctall=ftfirstefwhitt *(ftfirstefhispt / ftfirsteftotlt)\n",
    "gen wxlatinpct4yr=ftfirstefwhitt *(ftfirstefhispt / ftfirsteftotlt) if iclevel==1\n",
    "gen wxlatinpct2yr=ftfirstefwhitt *(ftfirstefhispt / ftfirsteftotlt) if iclevel==2\n",
    "\n",
    "foreach var in ftfirstefbkaat ftfirstefhispt ftfirstefwhitt {\n",
    "    gen `var'4yr =`var' if iclevel==1\n",
    "    gen `var'2yr =`var' if iclevel==2\n",
    "    rename `var' `var'all\n",
    "}\n",
    "\n",
    "collapse (sum) ftfirstefbkaat* ftfirstefhispt* ftfirstefwhitt* wx*, by(stabbr)\n",
    "\n",
    "foreach level in all 4yr 2yr {\n",
    "    gen wtblk`level'=100*(1/ftfirstefwhitt`level')*wxblackpct`level'\n",
    "    gen wtltn`level'=100*(1/ftfirstefwhitt`level')*wxlatinpct`level'\n",
    "    }\n",
    "\n",
    "foreach var in wtblk wtltn {\n",
    "    gen `var'dif=`var'4yr - `var'2yr\n",
    "    }\n",
    "drop if stabbr==\"PR\" | stabbr==\"GU\" | stabbr==\"VI\" | stabbr==\"PW\" | stabbr==\"AS\" | stabbr==\"MH\" | ///\n",
    "    stabbr==\"MP\" | stabbr==\"FM\"\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      "\n",
      "AVERAGE PERCENTAGE OF STUDENTS WHO ARE BLACK AT SCHOOLS OF AVERAGE WHITE STUDENTS\n",
      "\n",
      "NEGATIVE DIF = MORE SEGREGATION AT 4 YEARS\n",
      "\n",
      "\n",
      "     +-----------------------------------------------------+\n",
      "     | stabbr   wtblkall   wtblk4yr   wtblk2yr    wtblkdif |\n",
      "     |-----------------------------------------------------|\n",
      "  1. |     DC   8.160051   8.027805   50.49407   -42.46627 |\n",
      "  2. |     MS   29.23646    18.8968    37.6819    -18.7851 |\n",
      "  3. |     LA   22.06199    17.3809   35.27783   -17.89693 |\n",
      "  4. |     NV   6.262385   5.546621   20.06127   -14.51465 |\n",
      "  5. |     DE   12.76821    12.2592   26.31579   -14.05659 |\n",
      "     |-----------------------------------------------------|\n",
      "  6. |     GA   20.19677   18.37978   32.19826   -13.81849 |\n",
      "  7. |     FL   12.94791   11.31427   23.14557    -11.8313 |\n",
      "  8. |     AL      15.81   12.77365   22.01055   -9.236896 |\n",
      "  9. |     SC   15.19185   12.33119   21.16738   -8.836188 |\n",
      " 10. |     MN   6.650834   4.586222   11.85947   -7.273243 |\n",
      "     |-----------------------------------------------------|\n",
      " 11. |     OH   9.023042   7.758039   14.75504   -6.997003 |\n",
      " 12. |     MD   16.36755   13.91309   20.71609   -6.802998 |\n",
      " 13. |     KS   7.256457   5.180361     11.228   -6.047639 |\n",
      " 14. |     NC   13.17407   11.37457   17.14709   -5.772525 |\n",
      " 15. |     NY   10.39453   8.933758     14.282   -5.348247 |\n",
      "     |-----------------------------------------------------|\n",
      " 16. |     MA   7.261781    6.57321   11.59917   -5.025963 |\n",
      " 17. |     AK   3.075636   2.825446   7.826087   -5.000641 |\n",
      " 18. |     ND    3.06847   2.338536   7.261859   -4.923323 |\n",
      " 19. |     CT   10.23421   9.206125   13.71072     -4.5046 |\n",
      " 20. |     TX   10.36842   8.835945   13.15799   -4.322043 |\n",
      "     |-----------------------------------------------------|\n",
      " 21. |     VA   12.34995   11.22617   15.48958   -4.263407 |\n",
      " 22. |     PA   8.459246   7.796255   11.56563   -3.769378 |\n",
      " 23. |     IA   5.392296   4.308707   7.705851   -3.397144 |\n",
      " 24. |     IN   7.574893    7.10422   10.43579   -3.331568 |\n",
      " 25. |     AR   10.96457   10.11918   13.37797   -3.258789 |\n",
      "     |-----------------------------------------------------|\n",
      " 26. |     OK   6.946959   5.889439   9.111286   -3.221848 |\n",
      " 27. |     CO   3.702359   3.315228   6.272172   -2.956944 |\n",
      " 28. |     WI   3.514449   3.254804   5.246293   -1.991489 |\n",
      " 29. |     ME   4.195426   3.711651   5.566206   -1.854555 |\n",
      " 30. |     NJ   11.27956   10.22913   11.82722   -1.598093 |\n",
      "     |-----------------------------------------------------|\n",
      " 31. |     WY   2.056165   1.189832   2.710873   -1.521041 |\n",
      " 32. |     MT   1.052593   .9081497   2.071929   -1.163779 |\n",
      " 33. |     RI   6.159212   5.509083   6.607203    -1.09812 |\n",
      " 34. |     AZ   5.149086   4.799944   5.683426   -.8834825 |\n",
      " 35. |     TN    11.7814   11.38115   12.26196   -.8808079 |\n",
      "     |-----------------------------------------------------|\n",
      " 36. |     ID   1.130858   1.003973   1.669972   -.6659994 |\n",
      " 37. |     MI   9.140379   8.610386   9.268888   -.6585016 |\n",
      " 38. |     WA   3.461969      3.415   3.845394   -.4303939 |\n",
      " 39. |     NM   3.404748    2.92337     3.2998     -.37643 |\n",
      " 40. |     HI   1.122272   1.012795   1.290212   -.2774169 |\n",
      "     |-----------------------------------------------------|\n",
      " 41. |     CA   4.123574   3.890195    4.08511   -.1949148 |\n",
      " 42. |     MO   8.389151   8.330178   8.505709   -.1755304 |\n",
      " 43. |     UT    2.37149   2.363339   2.479086   -.1157472 |\n",
      " 44. |     OR   2.371685    2.34203    2.42571   -.0836799 |\n",
      " 45. |     NE      4.007    3.99305   4.051563   -.0585124 |\n",
      "     |-----------------------------------------------------|\n",
      " 46. |     VT   3.026593    3.06044   3.092783   -.0323434 |\n",
      " 47. |     SD   2.066488   2.208559   1.546924     .661635 |\n",
      " 48. |     IL   10.44952   11.02474   8.831899    2.192838 |\n",
      " 49. |     KY    7.94348   8.536641   6.307362     2.22928 |\n",
      " 50. |     WV   6.375092   6.858714   4.402847    2.455866 |\n",
      "     |-----------------------------------------------------|\n",
      " 51. |     NH   5.452057   5.926247   2.517992    3.408254 |\n",
      "     +-----------------------------------------------------+\n",
      "\n",
      "\n",
      "AVERAGE PERCENTAGE OF STUDENTS WHO ARE LATINX AT SCHOOLS OF AVERAGE WHITE STUDENTS\n",
      "\n",
      "NEGATIVE DIF = MORE SEGREGATION AT 4 YEARS\n",
      "\n",
      "\n",
      "     +-----------------------------------------------------+\n",
      "     | stabbr   wtltnall   wtltn4yr   wtltn2yr    wtltndif |\n",
      "     |-----------------------------------------------------|\n",
      "  1. |     UT   10.38656   9.295104   24.11692   -14.82181 |\n",
      "  2. |     AZ    28.5708   25.38136    38.4073   -13.02594 |\n",
      "  3. |     CT   15.90746   13.53997   26.32745   -12.78749 |\n",
      "  4. |     CA   35.12901   30.30294   43.01068   -12.70774 |\n",
      "  5. |     RI    13.8543   11.65094   22.36286   -10.71192 |\n",
      "     |-----------------------------------------------------|\n",
      "  6. |     CO   19.93675    18.4846   28.74978   -10.26518 |\n",
      "  7. |     MA   12.40188   11.00704   21.12858   -10.12154 |\n",
      "  8. |     ID   10.67924   9.198966   16.51328   -7.314309 |\n",
      "  9. |     TX   31.16372   29.04138   35.19803   -6.156651 |\n",
      " 10. |     NJ   20.95397   18.65822    24.6915   -6.033279 |\n",
      "     |-----------------------------------------------------|\n",
      " 11. |     KS   11.45641   9.516116   15.37239   -5.856276 |\n",
      " 12. |     NV   30.10714   29.84828   35.25485   -5.406567 |\n",
      " 13. |     NE   11.34039   10.14409   15.16205   -5.017957 |\n",
      " 14. |     WA   14.93427   14.69035   19.27377   -4.583411 |\n",
      " 15. |     VA   8.688777   7.586152   12.01419   -4.428035 |\n",
      "     |-----------------------------------------------------|\n",
      " 16. |     NC   8.967029   7.731534   12.10271   -4.371178 |\n",
      " 17. |     PA   7.738745    7.11963   11.29503   -4.175403 |\n",
      " 18. |     NY   14.44565   13.43379   17.40836   -3.974566 |\n",
      " 19. |     WY   9.838619    7.57166   11.43164   -3.859979 |\n",
      " 20. |     MN   6.397585   5.490373   8.687885   -3.197512 |\n",
      "     |-----------------------------------------------------|\n",
      " 21. |     HI   12.50108   11.98867   14.87272   -2.884051 |\n",
      " 22. |     OR   14.99177     13.892   16.73089   -2.838894 |\n",
      " 23. |     SC   6.347774   5.593571    8.13797   -2.544399 |\n",
      " 24. |     AL   4.611201   3.959932   5.984272    -2.02434 |\n",
      " 25. |     ND   3.816402   3.541508   5.562284   -2.020776 |\n",
      "     |-----------------------------------------------------|\n",
      " 26. |     IA    7.65251   7.012758   9.022149   -2.009391 |\n",
      " 27. |     AR   7.868807   7.206463   9.123419   -1.916956 |\n",
      " 28. |     WI    7.55795   7.345443   9.055984    -1.71054 |\n",
      " 29. |     GA   9.619567    9.43802   10.94451   -1.506491 |\n",
      " 30. |     MD   8.945261   8.501591   9.928039   -1.426448 |\n",
      "     |-----------------------------------------------------|\n",
      " 31. |     MI   6.207525   5.962396   7.276299   -1.313902 |\n",
      " 32. |     AK   8.166892   8.293816   9.565217   -1.271401 |\n",
      " 33. |     TN   5.816323   5.364221   6.445258   -1.081037 |\n",
      " 34. |     FL   21.82141    21.9146   22.74428   -.8296814 |\n",
      " 35. |     KY   4.098366   3.894994   4.653397   -.7584033 |\n",
      "     |-----------------------------------------------------|\n",
      " 36. |     IL   16.45521   16.33398   16.97967    -.645689 |\n",
      " 37. |     MO   5.514621   5.332731   5.960791   -.6280603 |\n",
      " 38. |     OK   10.76011   10.76579   11.21795   -.4521599 |\n",
      " 39. |     OH   4.753414    4.76771   5.199679   -.4319692 |\n",
      " 40. |     MS   3.143713   3.286769   3.013798    .2729709 |\n",
      "     |-----------------------------------------------------|\n",
      " 41. |     SD   4.246205   4.362169   3.822117    .5400517 |\n",
      " 42. |     ME   3.942917   4.262497   3.238344    1.024153 |\n",
      " 43. |     LA   6.325682   6.769492   4.941708    1.827784 |\n",
      " 44. |     WV   2.857785   3.286324   1.324919    1.961406 |\n",
      " 45. |     IN     7.6211   7.961912   5.437699    2.524213 |\n",
      "     |-----------------------------------------------------|\n",
      " 46. |     NH   6.911866   7.276081   4.662898    2.613183 |\n",
      " 47. |     VT    6.08221   6.244595   3.092783    3.151812 |\n",
      " 48. |     NM   52.60157   54.22335   51.01308    3.210278 |\n",
      " 49. |     MT   4.635062   5.153596   1.844221    3.309375 |\n",
      " 50. |     DC   11.79979   11.81223   7.608696     4.20353 |\n",
      "     |-----------------------------------------------------|\n",
      " 51. |     DE   10.63638   10.70591   5.263158    5.442749 |\n",
      "     +-----------------------------------------------------+\n"
     ]
    }
   ],
   "source": [
    "quietly set line 200\n",
    "\n",
    "quietly sort wtblkdif\n",
    "display \"AVERAGE PERCENTAGE OF STUDENTS WHO ARE BLACK AT SCHOOLS OF AVERAGE WHITE STUDENTS\"\n",
    "display \"NEGATIVE DIF = MORE SEGREGATION AT 4 YEARS\"\n",
    "list stabbr wtblk*\n",
    "\n",
    "quietly sort wtltndif\n",
    "display \"AVERAGE PERCENTAGE OF STUDENTS WHO ARE LATINX AT SCHOOLS OF AVERAGE WHITE STUDENTS\"\n",
    "display \"NEGATIVE DIF = MORE SEGREGATION AT 4 YEARS\"\n",
    "list stabbr wtltn*"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
